unit uAnkaufformular;

interface

uses
  Winapi.Windows, Winapi.Messages, System.SysUtils, System.Variants, System.Classes, Vcl.Graphics,
  Vcl.Controls, Vcl.Forms, Vcl.Dialogs, Vcl.Imaging.pngimage, Vcl.ExtCtrls,
  Vcl.StdCtrls, Vcl.ComCtrls, Vcl.Mask, AdvPageControl, StrUtils,
  FireDAC.Stan.Param, FireDAC.Phys.SQLite, Data.DB, FireDAC.Comp.DataSet, FireDAC.Comp.Client,
  System.Generics.Defaults, FireDAC.Stan.Intf, FireDAC.DApt, DateUtils, Vcl.Buttons, ShellApi;

type
  TfAnkaufformular = class(TForm)
    Panel1: TPanel;
    Panel3: TPanel;
    edSKU: TLabeledEdit;
    edReferenz: TLabeledEdit;
    dtpAnkaufsdatum: TDateTimePicker;
    Label1: TLabel;
    edAnkaufspreis: TLabeledEdit;
    Label2: TLabel;
    edModel: TLabeledEdit;
    edMarke: TLabeledEdit;
    edJahr: TLabeledEdit;
    cbBox: TCheckBox;
    cbPapiere: TCheckBox;
    edBezeichnung: TLabeledEdit;
    edGewicht: TLabeledEdit;
    edVersand: TLabeledEdit;
    Label3: TLabel;
    edGesamtbetrag: TLabeledEdit;
    Label6: TLabel;
    cbZahlungsarten: TComboBox;
    Label10: TLabel;
    cbZustand: TComboBox;
    Label9: TLabel;
    sbNextSKU: TSpeedButton;
    Panel4: TPanel;
    edKundenNr: TLabeledEdit;
    edVorname: TLabeledEdit;
    edNachname: TLabeledEdit;
    edStrasseHausNr: TLabeledEdit;
    edPLZ: TLabeledEdit;
    edWohnort: TLabeledEdit;
    edTelefon: TLabeledEdit;
    edHandy: TLabeledEdit;
    edEmail: TLabeledEdit;
    edAusweisnr: TLabeledEdit;
    dtpGeburtsdatum: TDateTimePicker;
    Label8: TLabel;
    sbNextKdNr: TSpeedButton;
    edKundensuche: TLabeledEdit;
    btnKundensuche: TButton;
    cbAddToInventarliste: TCheckBox;
    btnSaveAnkauf: TButton;
    procedure FormShow(Sender: TObject);
    procedure cbZustandSelect(Sender: TObject);
    procedure cbZahlungsartenSelect(Sender: TObject);
    procedure btnKundensucheClick(Sender: TObject);
    procedure edKundensucheKeyPress(Sender: TObject; var Key: Char);
    procedure edNachnameKeyUp(Sender: TObject; var Key: Word; Shift: TShiftState);
    procedure btnSaveAnkaufClick(Sender: TObject);
    procedure edAnkaufspreisChange(Sender: TObject);
    procedure edAnkaufspreisKeyPress(Sender: TObject; var Key: Char);
    procedure edPLZKeyPress(Sender: TObject; var Key: Char);
    procedure edTelefonKeyPress(Sender: TObject; var Key: Char);
    procedure sbNextSKUClick(Sender: TObject);
    procedure AdvPageControl1CanChange(Sender: TObject; FromPage, ToPage: Integer; var AllowChange: Boolean);
    procedure FormKeyDown(Sender: TObject; var Key: Word; Shift: TShiftState);
    procedure sbNextKdNrClick(Sender: TObject);
  private
    procedure LoadKundendatenBySuchbegriff(const AConnection: TFDConnection; const Suchbegriff: string);
    procedure CreateAnkaufsformularAsPDF;
  public
    { Public-Deklarationen }
  end;

var
  fAnkaufformular: TfAnkaufformular;
  KUNDENID: int64;
  SELZUSTAND, SELZAHLUNGSART: integer;
  KUNDENNR: string;
  NEUERKUNDE, KUNDENDATENEDITED: boolean;
  Ankaufformular_LastID: integer;

implementation

{$R *.dfm}

uses
  uMain, uFunctions, uDBFunctions;




procedure TfAnkaufformular.AdvPageControl1CanChange(Sender: TObject; FromPage,
  ToPage: Integer; var AllowChange: Boolean);
begin
 if(KUNDENID <= 0) then
 begin
   showmessage('Bitte geben Sie zuerst die Kundendaten ein' + sLineBreak + 'und klicken Sie anschließend auf den Buttonn "Speichern und Weiter"');
   AllowChange := false;
 end;
end;

procedure TfAnkaufformular.btnKundensucheClick(Sender: TObject);
begin
  LoadKundendatenBySuchbegriff(fMain.FDConnection1, edKundensuche.Text);
end;






procedure TfAnkaufformular.btnSaveAnkaufClick(Sender: TObject);
var
  Q: TFDQuery;
  TSGeburtsdatum, TSAnkaufsdatum: Int64;
  Ankaufspreis, Versandpreis, Gesamtpreis: Int64;
  Jahr: Integer;
  Box, Papiere: Integer;
  SKU, Ref, Marke, Model, Bezeichnung, Gewicht, Zustand, Zahlungsart: string;

  function TrimEdit(const AEdit: TLabeledEdit): string;
  begin
    Result := Trim(AEdit.Text);
  end;

begin
  // ================= VALIDIERUNG =================
  if TrimEdit(edKundenNr) = '' then
  begin
    ShowMessage('Bitte geben Sie eine KundenNr ein!');
    edKundenNr.SetFocus;
    Exit;
  end;

  if TrimEdit(edSKU) = '' then
  begin
    ShowMessage('Bitte eine SKU eingeben!');
    edSKU.SetFocus;
    Exit;
  end;

  if TrimEdit(edAnkaufspreis) = '' then
  begin
    ShowMessage('Bitte den Ankaufspreis eingeben!');
    edAnkaufspreis.SetFocus;
    Exit;
  end;

  if(Trim(edNachname.Text) = '') AND (Trim(edVorname.Text) = '') then
  begin
    if MessageDlg('Sie haben keinen Kundennammen eingegeben. Dieser sollte eingegeben werden damit das Ankaufsformular anständig erzeugt werden kann.' + sLineBreak + sLineBreak + 'Wollen Sie den Namen des Kunden jetzt eingeben?', mtConfirmation, [mbYes, mbNo], 0) = mrYes then
    begin
      edNachname.SetFocus;
      exit;
    end;
  end;

  // ================= DATEN =================
  if dtpGeburtsdatum.Checked then
    TSGeburtsdatum := DateTimeToUnix(dtpGeburtsdatum.Date, False)
  else
    TSGeburtsdatum := 0;


  if dtpAnkaufsdatum.Checked then
    TSAnkaufsdatum := DateTimeToUnix(dtpAnkaufsdatum.Date, False)
  else
    TSAnkaufsdatum := 0;


  Box      := Ord(cbBox.Checked);
  Papiere := Ord(cbPapiere.Checked);

  SKU         := TrimEdit(edSKU);
  Ref         := TrimEdit(edReferenz);
  Marke       := TrimEdit(edMarke);
  Model       := TrimEdit(edModel);
  Bezeichnung := TrimEdit(edBezeichnung);
  Gewicht     := TrimEdit(edGewicht);
  Zustand     := cbZustand.Text;
  Zahlungsart := cbZahlungsarten.Text;
  Jahr        := StrToIntDef(TrimEdit(edJahr), 0);

  Ankaufspreis := EditToCentSafe(edAnkaufspreis);
  Versandpreis := EditToCentSafe(edVersand);
  Gesamtpreis  := EditToCentSafe(edGesamtbetrag);

  // ================= DB =================
  Q := TFDQuery.Create(nil);
  try
    Q.Connection := fMain.FDConnection1;
    fMain.FDConnection1.StartTransaction;
    try
      // ================= KUNDE =================
      if NEUERKUNDE and (KUNDENID = 0) then
      begin
        // ---------- INSERT ----------
        Q.SQL.Text :=
          'INSERT INTO kundendaten ' +
          '(kundenNr, Nachname, Vorname, StrasseHausNr, PLZ, Ort, Telefon, Handy, Email, AusweisNr, Geburtsdatum) ' +
          'VALUES (:KNR,:NACH,:VOR,:STR,:PLZ,:ORT,:TEL,:HANDY,:MAIL,:AUSW,:GEB)';

        Q.ParamByName('KNR').AsString  := TrimEdit(edKundenNr);
        Q.ParamByName('NACH').AsString := TrimEdit(edNachname);
        Q.ParamByName('VOR').AsString  := TrimEdit(edVorname);
        Q.ParamByName('STR').AsString  := TrimEdit(edStrasseHausNr);
        Q.ParamByName('PLZ').AsString  := TrimEdit(edPLZ);
        Q.ParamByName('ORT').AsString  := TrimEdit(edWohnort);
        Q.ParamByName('TEL').AsString  := TrimEdit(edTelefon);
        Q.ParamByName('HANDY').AsString:= TrimEdit(edHandy);
        Q.ParamByName('MAIL').AsString := TrimEdit(edEmail);

        if TrimEdit(edAusweisNr) = '' then
        begin
          Q.ParamByName('AUSW').DataType := ftString;
          Q.ParamByName('AUSW').Clear;
        end
        else
          Q.ParamByName('AUSW').AsString := TrimEdit(edAusweisNr);

        Q.ParamByName('GEB').AsLargeInt := TSGeburtsdatum;
        Q.ExecSQL;

        Q.SQL.Text := 'SELECT last_insert_rowid()';
        Q.Open;
        KUNDENID := Q.Fields[0].AsInteger;
        Q.Close;
      end
      else if (not NEUERKUNDE) and (KUNDENID > 0) then
      begin
        // ---------- UPDATE ----------
        Q.SQL.Text :=
          'UPDATE kundendaten SET ' +
          'kundenNr=:KNR, Nachname=:NACH, Vorname=:VOR, StrasseHausNr=:STR, ' +
          'PLZ=:PLZ, Ort=:ORT, Telefon=:TEL, Handy=:HANDY, Email=:MAIL, ' +
          'AusweisNr=:AUSW, Geburtsdatum=:GEB ' +
          'WHERE id=:ID';

        Q.ParamByName('ID').AsInteger   := KUNDENID;
        Q.ParamByName('KNR').AsString   := TrimEdit(edKundenNr);
        Q.ParamByName('NACH').AsString  := TrimEdit(edNachname);
        Q.ParamByName('VOR').AsString   := TrimEdit(edVorname);
        Q.ParamByName('STR').AsString   := TrimEdit(edStrasseHausNr);
        Q.ParamByName('PLZ').AsString   := TrimEdit(edPLZ);
        Q.ParamByName('ORT').AsString   := TrimEdit(edWohnort);
        Q.ParamByName('TEL').AsString   := TrimEdit(edTelefon);
        Q.ParamByName('HANDY').AsString := TrimEdit(edHandy);
        Q.ParamByName('MAIL').AsString  := TrimEdit(edEmail);

       if TrimEdit(edAusweisNr) = '' then
        begin
          Q.ParamByName('AUSW').DataType := ftString;
          Q.ParamByName('AUSW').Clear;
        end
        else
          Q.ParamByName('AUSW').AsString := TrimEdit(edAusweisNr);

        Q.ParamByName('GEB').AsLargeInt := TSGeburtsdatum;
        Q.ExecSQL;
      end;

      // ================= ANKAUF =================
      Q.SQL.Text :=
        'INSERT INTO kundenankauf ' +
        '(kundenID, Ankaufsdatum, SKU, Ref, Box, Papiere, Marke, Model, Jahr, Kaufpreis, Zustand, ' +
        'Bezeichnung, Gewicht, Versand, Zahlungsart, SavedInInventar) ' +
        'VALUES (:KD,:DAT,:SKU,:REF,:BOX,:PAP,:MAR,:MOD,:JAHR,:KP,:ZUST,:BEZ,:GEW,:VERS,:ZAHL,:SAVE)';

      Q.ParamByName('KD').AsInteger    := KUNDENID;
      Q.ParamByName('DAT').AsLargeInt  := TSAnkaufsdatum;
      Q.ParamByName('SKU').AsString    := SKU;
      Q.ParamByName('REF').AsString    := Ref;
      Q.ParamByName('BOX').AsInteger   := Box;
      Q.ParamByName('PAP').AsInteger   := Papiere;
      Q.ParamByName('MAR').AsString    := Marke;
      Q.ParamByName('MOD').AsString    := Model;
      Q.ParamByName('JAHR').AsInteger  := Jahr;
      Q.ParamByName('KP').AsLargeInt   := Ankaufspreis;
      Q.ParamByName('ZUST').AsString   := Zustand;
      Q.ParamByName('BEZ').AsString    := Bezeichnung;
      Q.ParamByName('GEW').AsString    := Gewicht;
      Q.ParamByName('VERS').AsLargeInt := Versandpreis;
      Q.ParamByName('ZAHL').AsString   := Zahlungsart;
      Q.ParamByName('SAVE').AsInteger  := Ord(cbAddToInventarliste.Checked);
      Q.ExecSQL;

      Q.SQL.Text := 'SELECT last_insert_rowid()';
      Q.Open;
      Ankaufformular_LastID := Q.Fields[0].AsInteger;
      Q.Close;

      // ================= INVENTAR =================
      if cbAddToInventarliste.Checked then
      begin
        Q.SQL.Text :=
          'INSERT INTO inventar ' +
          '(Einkaufsdatum, SKU, Einkaufswert, EinkaufBemerkung, Einheit, AnkaufformularID) ' +
          'VALUES (:DAT,:SKU,:WERT,:BEM,:EIN,:AID)';

        Q.ParamByName('DAT').AsLargeInt  := TSAnkaufsdatum;
        Q.ParamByName('SKU').AsString    := SKU;
        Q.ParamByName('WERT').AsLargeInt := Gesamtpreis;
        Q.ParamByName('BEM').AsString    := 'Ankauf: ' + Ankaufformular_LastID.ToString + ' - ' + Bezeichnung;
        Q.ParamByName('EIN').AsString    := Gewicht;
        Q.ParamByName('AID').AsInteger   := Ankaufformular_LastID;
        Q.ExecSQL;
      end;

      fMain.FDConnection1.Commit;
    except
      fMain.FDConnection1.Rollback;
      raise;
    end;
  finally
    Q.Free;
  end;

  fMain.LoadInventarToListView;

  CreateAnkaufsformularAsPDF;

  Close;
end;








procedure TfAnkaufformular.CreateAnkaufsformularAsPDF;
var
  stltemp: TStringList;
  i: integer;
  filename, filenameTemp: string;
  stlHtmlHeader, stlHtmlFooter, stlContent: TStringList;
  resHtmlHeader, resHtmlFooter, resContent: TResourceStream;

  nachname, vorname, strassehausnr, plz, ort: string;
  ankaufsnr, ankaufdatum: string;

  ref, box, papiere, marke, model, jahr, kaufbetrag: string;
  s: string;
  zustand, bezeichnung, gewicht, gesamtbetrag, zahlungsart, sku: string;
  versandbetrag, kurzbezeichnung: string;
begin
  //kundennr := trim(edKundenNr.Text);
  nachname := trim(edNachname.Text);
  vorname := trim(edVorname.Text);
  strassehausnr := trim(edStrasseHausNr.Text);
  plz := trim(edPLZ.Text);
  ort := trim(edWohnort.Text);
  ankaufsnr := IntToStr(Ankaufformular_LastID);
  ankaufdatum := trim(DateToStr(dtpAnkaufsdatum.Date));

  zustand := trim(cbZustand.Text);
  bezeichnung := trim(edBezeichnung.Text);
  gewicht := trim(edGewicht.Text);
  versandbetrag := trim(edVersand.Text);
  gesamtbetrag := trim(edGesamtbetrag.Text);
  kurzbezeichnung := 'Kurzbezeichnung';
  zahlungsart := trim(cbZahlungsarten.Text);
  sku := trim(edSKU.Text);

  ref := trim(edReferenz.Text);
  if(cbBox.Checked) then box := 'X' else box := '-';
  if(cbPapiere.Checked) then papiere := 'X' else papiere := '-';
  marke := edMarke.Text;
  model := edModel.Text;

  if(Trim(marke)<>'') then
    if(Trim(model)<>'') then
      s := marke + ' / ' + model
    else
      s := marke
  else
    s := 'unbekannt';

  jahr := edJahr.Text;
  kaufbetrag := edAnkaufspreis.Text;


//Hier nur das was einmal für alle Seiten geladen werden muss (HtmlHeader, HtmlFooter)
  stlTemp := nil;
  try
    stlTemp := TStringList.Create;

//HEADER START
    resHtmlHeader := TResourceStream.Create(HInstance, 'ANKAUFSFORMULAR_HEADER', 'TXT');
    stlHtmlHeader := TStringList.Create;
    try
      stlHtmlHeader.LoadFromStream(resHtmlHeader);

      stlHtmlHeader.Text := StringReplace(stlHtmlHeader.Text, '#KUNDEVORNAME', vorname, [rfReplaceAll]);
      stlHtmlHeader.Text := StringReplace(stlHtmlHeader.Text, '#KUNDENACHNAME', nachname, [rfReplaceAll]);
      stlHtmlHeader.Text := StringReplace(stlHtmlHeader.Text, '#KUNDESTRASSEHAUSNR', strassehausnr, [rfReplaceAll]);
      stlHtmlHeader.Text := StringReplace(stlHtmlHeader.Text, '#KUNDEPLZ', plz, [rfReplaceAll]);
      stlHtmlHeader.Text := StringReplace(stlHtmlHeader.Text, '#KUNDENORT', ort, [rfReplaceAll]);

      s := FIRMENNAME + ' - ' + FIRMASTRASSE + ', ' + FIRMAORT;

      stlHtmlHeader.Text := StringReplace(stlHtmlHeader.Text, '#BRIEFFENSTERFIRMENDATEN', s, [rfReplaceAll]);

      stlHtmlHeader.Text := StringReplace(stlHtmlHeader.Text, '#INHABERNAME', FIRMENINHABER, [rfReplaceAll]);
      stlHtmlHeader.Text := StringReplace(stlHtmlHeader.Text, '#FIRMENNAME', FIRMENNAME, [rfReplaceAll]);
      stlHtmlHeader.Text := StringReplace(stlHtmlHeader.Text, '#FIRMASTRASSEHAUSNR', FIRMASTRASSE, [rfReplaceAll]);
      stlHtmlHeader.Text := StringReplace(stlHtmlHeader.Text, '#FIRMAPLZ', FIRMAPLZ, [rfReplaceAll]);
      stlHtmlHeader.Text := StringReplace(stlHtmlHeader.Text, '#FIRMAORT', FIRMAORT, [rfReplaceAll]);

      stlHtmlHeader.Text := StringReplace(stlHtmlHeader.Text, '#ANKAUFSNUMMER', IntToStr(ExtractNumber(sku)), [rfReplaceAll]);
      stlHtmlHeader.Text := StringReplace(stlHtmlHeader.Text, '#ANKAUFDATUM', ankaufdatum, [rfReplaceAll]);

      stltemp.Add(stlHtmlHeader.Text);
    finally
      stlHtmlHeader.Free;
      resHtmlHeader.Free;
    end;
//HEADER ENDE


//CONTENT START
    resContent := TResourceStream.Create(HInstance, 'ANKAUFSFORMULAR_CONTENT', 'TXT');
    stlContent := TStringList.Create;
    try
      stlContent.LoadFromStream(resContent);
      stlContent.Text := StringReplace(stlContent.Text, '#REF', ref, [rfReplaceAll]);
      stlContent.Text := StringReplace(stlContent.Text, '#BOX', box, [rfReplaceAll]);
      stlContent.Text := StringReplace(stlContent.Text, '#PAPIERE', papiere, [rfReplaceAll]);
      stlContent.Text := StringReplace(stlContent.Text, '#MARKEMODEL', s, [rfReplaceAll]);
      stlContent.Text := StringReplace(stlContent.Text, '#JAHR', jahr, [rfReplaceAll]);
      stlContent.Text := StringReplace(stlContent.Text, '#KAUFPREIS', kaufbetrag, [rfReplaceAll]);
      stltemp.Add(stlContent.Text);
    finally
      resContent.Free;
      stlContent.Free;
    end;
//CONTENT ENDE


//FOOTER START
    resHtmlFooter := TResourceStream.Create(HInstance, 'ANKAUFSFORMULAR_FOOTER', 'TXT');
    stlHtmlFooter := TStringList.Create;
    try
      stlHtmlFooter.LoadFromStream(resHtmlFooter);

      stlHtmlFooter.Text := StringReplace(stlHtmlFooter.Text, '#ZUSTAND', zustand, [rfReplaceAll]);
      stlHtmlFooter.Text := StringReplace(stlHtmlFooter.Text, '#BEZEICHNUNG', bezeichnung, [rfReplaceAll]);
      stlHtmlFooter.Text := StringReplace(stlHtmlFooter.Text, '#GEWICHT', gewicht, [rfReplaceAll]);
      stlHtmlFooter.Text := StringReplace(stlHtmlFooter.Text, '#VERSANDBETRAG', versandbetrag, [rfReplaceAll]);
      stlHtmlFooter.Text := StringReplace(stlHtmlFooter.Text, '#GESAMTBETRAG', gesamtbetrag, [rfReplaceAll]);
      stlHtmlFooter.Text := StringReplace(stlHtmlFooter.Text, '#KURZBEZEICHNUNG', kurzbezeichnung, [rfReplaceAll]);
      stlHtmlFooter.Text := StringReplace(stlHtmlFooter.Text, '#ZAHLUNGSART', zahlungsart, [rfReplaceAll]);
      stlHtmlFooter.Text := StringReplace(stlHtmlFooter.Text, '#SKU', sku, [rfReplaceAll]);
      stlHtmlFooter.Text := StringReplace(stlHtmlFooter.Text, '#INHABERNAME', FIRMENINHABER, [rfReplaceAll]);
      stlHtmlFooter.Text := StringReplace(stlHtmlFooter.Text, '#FIRMAUSTID',FIRMAUMSATZSTEUERID, [rfReplaceAll]);
      stlHtmlFooter.Text := StringReplace(stlHtmlFooter.Text, '#FIRMAIBAN', FIRMAIBAN, [rfReplaceAll]);

      stltemp.Add(stlHtmlFooter.Text);
    finally
      stlHtmlFooter.Free;
      resHtmlFooter.Free;
    end;
//FOOTER ENDE


  //Alle Umlaute in der StringList ersetzen durch html code
    for i := 0 to stlTemp.Count - 1 do
    begin
      stlTemp[i] := ReplaceUmlauteWithHtmlEntities(stlTemp[i]);
    end;


    filenameTemp := 'Ankaufsformular';

    if(Ankaufformular_LastID > 0) then
      filenameTemp := filenameTemp + '_' + IntToStr(ExtractNumber(sku)); //IntToStr(Ankaufformular_LastID);

    //Dateiname für zu speichernde Datei erzeugen
    if(trim(edNachname.Text) <> '') then
      if(Trim(edVorname.Text) <> '') then
        filenameTemp := filenameTemp + '_' + trim(edNachname.Text) + '_' + trim(edVorname.Text)
      else
        filenameTemp := filenameTemp + '_' + trim(edNachname.Text);


   filename := filenameTemp;

   // filename := 'Ankaufsformular_' + IntToStr(Ankaufformular_LastID) + ' _ ' + edNachname.Text + '_' + edVorname.Text;

    //Aus Resource-Datei temporäre Html-Datei und daraus eine PDF-Datei im TEMP Verzeichnis erzeugen

    CreateHtmlAndPdfFileFromResource(filename, stlTemp);



    //PDF Datei aus Temp Verzeichnis im Zielverzeichnis speichern
   // SpeicherePDFDatei(filename, PATH);
  finally
    stlTemp.Free;
  end;
end;
















procedure TfAnkaufformular.cbZahlungsartenSelect(Sender: TObject);
var
  i: integer;
begin
  i := cbZahlungsarten.ItemIndex;
  if i <> -1 then
  begin
    SELZAHLUNGSART := Integer(cbZahlungsarten.Items.Objects[i]);
  end;
end;




procedure TfAnkaufformular.cbZustandSelect(Sender: TObject);
var
  i: integer;
begin
  i := cbZustand.ItemIndex;
  if i <> -1 then
  begin
    SELZUSTAND := Integer(cbZustand.Items.Objects[i]);
  end;
end;




procedure TfAnkaufformular.edAnkaufspreisChange(Sender: TObject);
var
  Kaufpreis, Versand, Gesamt: Double;
begin
  // Werte aus den Edit-Feldern auslesen
  Kaufpreis := StrToFloatDef(Trim(edAnkaufspreis.Text), 0);
  Versand   := StrToFloatDef(Trim(edVersand.Text), 0);

  // Summe berechnen
  Gesamt := Kaufpreis + Versand;

  // Ergebnis anzeigen
  edGesamtbetrag.Text := FormatFloat('0.00', Gesamt);
end;





procedure TfAnkaufformular.edAnkaufspreisKeyPress(Sender: TObject; var Key: Char);
begin
// Ziffern erlauben
  if CharInSet(Key, ['0'..'9']) then
    Exit;

  // Komma erlauben (nur einmal)
  if (Key = ',') and (Pos(',', (Sender as TLabeledEdit).Text) = 0) then
    Exit;

  // Backspace erlauben
  if Key = #8 then
    Exit;

  // Enter erlauben
  if Key = #13 then
    Exit;

  if Key = '.' then
  begin
    Key := ',';
    if Pos(',', (Sender as TLabeledEdit).Text) > 0 then
      Key := #0;
    Exit;
  end;

  if (Key = '-') and ((Sender as TLabeledEdit).SelStart = 0)
   and (Pos('-', (Sender as TLabeledEdit).Text) = 0) then
  Exit;

  // Alles andere blockieren
  Key := #0;
end;





procedure TfAnkaufformular.edKundensucheKeyPress(Sender: TObject; var Key: Char);
begin
  if Key = #13 then
  begin
    Key := #0;
    btnKundensucheClick(nil);
  end;
end;




procedure TfAnkaufformular.edNachnameKeyUp(Sender: TObject; var Key: Word; Shift: TShiftState);
begin
  KUNDENDATENEDITED := true;
end;



procedure TfAnkaufformular.edPLZKeyPress(Sender: TObject; var Key: Char);
begin
// Ziffern erlauben
  if CharInSet(Key, ['0'..'9']) then
    Exit;

  // Backspace erlauben
  if Key = #8 then
    Exit;

  // Enter erlauben
  if Key = #13 then
    Exit;

  // Alles andere blockieren
  Key := #0;
end;

procedure TfAnkaufformular.edTelefonKeyPress(Sender: TObject; var Key: Char);
begin
// Ziffern erlauben
  if CharInSet(Key, ['0'..'9']) then
    Exit;

  // - erlauben (nur einmal)
  if (Key = '-') and (Pos('-', (Sender as TLabeledEdit).Text) = 0) then
    Exit;

  // Backspace erlauben
  if Key = #8 then
    Exit;

  // Enter erlauben
  if Key = #13 then
    Exit;

  if Key = '.' then
  begin
    Key := ',';
    if Pos(',', (Sender as TLabeledEdit).Text) > 0 then
      Key := #0;
    Exit;
  end;

  // Alles andere blockieren
  Key := #0;
end;

procedure TfAnkaufformular.FormKeyDown(Sender: TObject; var Key: Word; Shift: TShiftState);
begin
  if Key = VK_ESCAPE then
  begin
    Key := 0;
    Close;
  end;
end;

procedure TfAnkaufformular.FormShow(Sender: TObject);
var
  s: string;
begin
  KUNDENDATENEDITED := false;
  NEUERKUNDE := true;
  KUNDENID := 0;

  Ankaufformular_LastID := 0;

  LoadZustaendeFromDB(fMain.FDConnection1, cbZustand);
  LoadZahlungsartenFromDB(fMain.FDConnection1, cbZahlungsarten);

  edKundensuche.Clear;
  edKundenNr.Clear;
  edNachname.Clear;
  edVorname.Clear;
  edStrasseHausNr.Clear;
  edPLZ.Clear;
  edWohnort.Clear;
  edTelefon.Clear;
  edHandy.Clear;
  edEmail.Clear;
  edAusweisNr.Clear;
  dtpGeburtsdatum.Date := StrToDate('01.01.1970');
  dtpGeburtsdatum.Checked := false;


  //Ankaufsformular
  edSKU.Clear;
  edAnkaufspreis.Clear;
  edReferenz.Clear;
  edJahr.Clear;
  edMarke.Clear;
  edModel.Clear;
  cbBox.Checked := false;
  cbPapiere.Checked := false;
  edBezeichnung.Clear;
  edGewicht.Clear;
  edVersand.Clear;
  edGesamtbetrag.Clear;
  cbZustand.ItemIndex := -1;
  cbZahlungsarten.ItemIndex := -1;
  cbAddToInventarliste.Checked := false;
  dtpAnkaufsdatum.Date := now;
  dtpAnkaufsdatum.Checked := true;


  edKundensuche.SetFocus;

  s := GetNextSKU(fMain.FDConnection1, true); //Warenankauf
  if(trim(s) = '') then
    edSKU.Text := STARTSKU_ANKAUF
  else
    edSKU.Text := trim(s);

  edKundenNr.Text := GetNextKdNr(fMain.FDConnection1);
end;







procedure TfAnkaufformular.LoadKundendatenBySuchbegriff(const AConnection: TFDConnection; const Suchbegriff: string);
var
  Q: TFDQuery;
  DT: TDateTime;
  GeburtsUnix: Int64;
  IsDatum: Boolean;
begin
  if Trim(Suchbegriff) = '' then
  begin
    ShowMessage('Nach welchem Kunden suchen Sie?');
    Exit;
  end;

  // Prüfen, ob Suchbegriff ein Datum ist
  IsDatum := TryStrToDate(Suchbegriff, DT);
  if IsDatum then
    GeburtsUnix := DateTimeToUnix(DT, False);

  Q := TFDQuery.Create(nil);
  try
    Q.Connection := AConnection;

    Q.SQL.Text :=
      'SELECT id, kundenNr, Nachname, Vorname, StrasseHausNr, PLZ, Ort, ' +
      'Telefon, Handy, Email, AusweisNr, Geburtsdatum ' +
      'FROM kundendaten ' +
      'WHERE UPPER(kundenNr) = UPPER(:SUCH) OR ' +
            'UPPER(Nachname) = UPPER(:SUCH) OR ' +
            'UPPER(AusweisNr) = UPPER(:SUCH)';

    if IsDatum then
      Q.SQL.Text := Q.SQL.Text + ' OR Geburtsdatum = :GEBURT';

    Q.ParamByName('SUCH').AsString := Trim(Suchbegriff);

    if IsDatum then
      Q.ParamByName('GEBURT').AsLargeInt := GeburtsUnix;

    Q.Open;

    // --- Keine Treffer ---
    if Q.RecordCount = 0 then
    begin
      ShowMessage('Es wurde kein Kunde anhand Ihres Suchbegriffs gefunden.');

      NEUERKUNDE := true;
      KUNDENID   := 0;

      dtpGeburtsdatum.Checked := False;

      edKundenNr.Text := GetNextKdNr(fMain.FDConnection1);
      edNachname.Clear;
      edVorname.Clear;
      edStrasseHausNr.Clear;
      edPLZ.Clear;
      edWohnort.Clear;
      edTelefon.Clear;
      edHandy.Clear;
      edEmail.Clear;
      edAusweisNr.Clear;
      dtpGeburtsdatum.Date := StrToDate('01.01.1970');
      dtpGeburtsdatum.Checked := false;

      sbNextKdNr.Visible := true;

      Exit;
    end
    else
    begin
      NEUERKUNDE := false;
    end;

    // --- Mehrere Treffer ---
    if Q.RecordCount > 1 then
    begin
      ShowMessage(
        'Mehrere Kunden entsprechen diesem Suchbegriff.' + sLineBreak +
        'Bitte präzisieren (z. B. KundenNr oder AusweisNr).'
      );
      sbNextKdNr.Visible := false;
      Exit;
    end;

    // --- Genau ein Treffer ---
    Q.First;

    KUNDENID             := Q.FieldByName('id').AsInteger;
    KUNDENNR             := Q.FieldByName('kundenNr').AsString;

    edKundenNr.Text      := Q.FieldByName('kundenNr').AsString;
    edNachname.Text      := Q.FieldByName('Nachname').AsString;
    edVorname.Text       := Q.FieldByName('Vorname').AsString;
    edStrasseHausNr.Text := Q.FieldByName('StrasseHausNr').AsString;
    edPLZ.Text           := Q.FieldByName('PLZ').AsString;
    edWohnort.Text       := Q.FieldByName('Ort').AsString;
    edTelefon.Text       := Q.FieldByName('Telefon').AsString;
    edHandy.Text         := Q.FieldByName('Handy').AsString;
    edEmail.Text         := Q.FieldByName('Email').AsString;
    edAusweisNr.Text     := Q.FieldByName('AusweisNr').AsString;

    sbNextKdNr.Visible := false;

    //UnixTime → TDateTime
    if Q.FieldByName('Geburtsdatum').AsLargeInt > 0 then
    begin
      dtpGeburtsdatum.Date :=
        UnixToDateTime(Q.FieldByName('Geburtsdatum').AsLargeInt, False);
      dtpGeburtsdatum.Checked := True;
    end
    else
      dtpGeburtsdatum.Checked := False;

  finally
    Q.Free;
  end;
end;






procedure TfAnkaufformular.sbNextKdNrClick(Sender: TObject);
var
  s: string;
begin
  s := GetNextKdNr(fMain.FDConnection1);
  if(trim(s) = '') then
    edKundenNr.Text := STARTKDNR_ANKAUF
  else
    edKundenNr.Text := trim(s);
end;






procedure TfAnkaufformular.sbNextSKUClick(Sender: TObject);
var
  s: string;
begin
  s := GetNextSKU(fMain.FDConnection1, true);
  if(trim(s) = '') then
    edSKU.Text := STARTSKU_ANKAUF
  else
    edSKU.Text := trim(s);
end;

end.
