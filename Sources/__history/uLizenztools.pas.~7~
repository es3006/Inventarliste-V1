unit uLizenzTools;

interface

function GetHWID: string;
function LizenzCodeGespeichert: string;
function eMailGespeichert: string;
procedure LizenzSpeichern(const Email, Code: string);
procedure LizenzCodeLoeschen;
procedure EmailLoeschen;
function PruefeLizenzOnline(const Lizenzcode, Email, HWID, Programmname: string): string;


implementation

uses
  Windows, SysUtils, AnsiStrings, IniFiles, IdHTTP, IdSSL, IdSSLOpenSSL, NB30, Dialogs,
  IdSSLOpenSSLHeaders, uMain, IdURI;



function Netbios(p: PNCB): Byte; stdcall; external 'netapi32.dll';



function GetVolumeSerial: string;
var
  VolumeSerial: DWORD;
  Dummy1, Dummy2: DWORD;
  VolName, FSName: array[0..MAX_PATH] of Char;
begin
  if GetVolumeInformation('C:\', VolName, MAX_PATH,
    @VolumeSerial, Dummy1, Dummy2, FSName, MAX_PATH) then
    Result := IntToHex(VolumeSerial, 8)
  else
    Result := '00000000';
end;



function GetMacAddress: string;
var
  Adapter: TAdapterStatus;
  NCB: TNCB;
  LanaEnum: TLanaEnum;
  i: Integer;
begin
  Result := '';
  FillChar(NCB, SizeOf(NCB), 0);
  NCB.ncb_command := AnsiChar(NCBENUM);
  NCB.ncb_buffer := @LanaEnum;
  NCB.ncb_length := SizeOf(LanaEnum);

  if Netbios(@NCB) <> 0 then Exit;

  for i := 0 to Ord(LanaEnum.length) - 1 do
  begin
    FillChar(NCB, SizeOf(NCB), 0);
    NCB.ncb_command := AnsiChar(NCBRESET);
    NCB.ncb_lana_num := LanaEnum.lana[i];
    if Netbios(@NCB) <> 0 then Continue;

    FillChar(NCB, SizeOf(NCB), 0);
    NCB.ncb_command := AnsiChar(NCBASTAT);
    NCB.ncb_lana_num := LanaEnum.lana[i];
    AnsiStrings.StrPCopy(NCB.ncb_callname, '*');
    NCB.ncb_buffer := @Adapter;
    NCB.ncb_length := SizeOf(Adapter);

    if Netbios(@NCB) = 0 then
    begin
      Result := Format('%.2X-%.2X-%.2X-%.2X-%.2X-%.2X', [
        Byte(Adapter.adapter_address[0]),
        Byte(Adapter.adapter_address[1]),
        Byte(Adapter.adapter_address[2]),
        Byte(Adapter.adapter_address[3]),
        Byte(Adapter.adapter_address[4]),
        Byte(Adapter.adapter_address[5])
      ]);
      Exit;
    end;
  end;

  if Result = '' then
    Result := '00-00-00-00-00-00';
end;



function GetComputerNameID: string;
var
  Buffer: array[0..MAX_COMPUTERNAME_LENGTH + 1] of Char;
  Size: DWORD;
begin
  Size := Length(Buffer);
  if GetComputerName(Buffer, Size) then
    Result := string(Buffer)
  else
    Result := 'UnknownPC';
end;



function GetHWID: string;
begin
  Result := Format('%s-%s-%s',
    [GetVolumeSerial, GetMacAddress, GetComputerNameID]);
end;



function LizenzCodeGespeichert: string;
var
  Ini: TIniFile;
begin
  Ini := TIniFile.Create(uMain.PATH + 'settings.ini');
  try
    Result := Ini.ReadString('Lizenz', 'Code', '');
  finally
    Ini.Free;
  end;
end;


function eMailGespeichert: string;
var
  Ini: TIniFile;
begin
  Ini := TIniFile.Create(uMain.PATH + 'settings.ini');
  try
    Result := Ini.ReadString('Lizenz', 'Email', '');
  finally
    Ini.Free;
  end;
end;





procedure LizenzSpeichern(const Email, Code: string);
var
  Ini: TIniFile;
begin
  Ini := TIniFile.Create(uMain.PATH + 'settings.ini');
  try
    Ini.WriteString('Lizenz', 'Email', Email);
    Ini.WriteString('Lizenz', 'Code', Code);
  finally
    Ini.Free;
  end;
end;







procedure LizenzCodeLoeschen;
var
  Ini: TIniFile;
begin
  Ini := TIniFile.Create(uMain.PATH + 'settings.ini');
  try
    Ini.DeleteKey('Lizenz', 'Code');
  finally
    Ini.Free;
  end;
end;



procedure EmailLoeschen;
var
  Ini: TIniFile;
begin
  Ini := TIniFile.Create(uMain.PATH + 'settings.ini');
  try
    Ini.DeleteKey('Lizenz', 'Email');
  finally
    Ini.Free;
  end;
end;






function PruefeLizenzOnline(const Lizenzcode, Email, HWID, Programmname: string): string;
var
  HTTP: TIdHTTP;
  SSL: TIdSSLIOHandlerSocketOpenSSL;
  URL: string;
begin
  Result := 'ERROR'; // Standard: Server nicht erreichbar

  HTTP := TIdHTTP.Create(nil);
  SSL := TIdSSLIOHandlerSocketOpenSSL.Create(nil);
  try
    SSL.SSLOptions.Method := sslvTLSv1_2;
    HTTP.IOHandler := SSL;
    HTTP.ReadTimeout := 5000;
    HTTP.HandleRedirects := True;

    URL := 'https://lizenz.ensacom.de/api/check_license.php'
         + '?code=' + Lizenzcode
         + '&email='+ Email
         + '&hwid=' + HWID
         + '&app=' + Programmname;

    try
      Result := Trim(HTTP.Get(URL)); // OK, INVALID, EXPIRED usw.
    except
      on E: Exception do
      begin
        ShowMessage('Lizenzserver nicht erreichbar: ' + E.Message);
        Result := 'ERROR';
      end;
    end;

  finally
    SSL.Free;
    HTTP.Free;
  end;
end;




end.
